use crate::ast_infra::item::*;
use koopa::ir::BinaryOp;
use koopa::ir::Type;

grammar;

match {
  r"\s*" => {},
  r"//[^\n\r]*[\n\r]*" => {},
  r"/\*[^*]*\*+(?:[^/*][^*]*\*+)*/" => {},
  _
}

Comma<T>: Vec<T> = {
    <mut v:(<T> ",")*> <e: T> => {
        v.push(e);
        v
    }
}

pub CompUnit: CompUnit = <func_def: FuncDef> => CompUnit {<>};

Decl: Decl = {
    <c_decl: ConstDecl> => Decl::ConstDecl(c_decl),
    <v_decl: VarDecl> => Decl::VarDecl(v_decl),
};

BType: Type = "int" => Type::get_i32();

ConstDecl: ConstDecl = "const" <btype: BType> <const_defs: Comma<ConstDef>> ";" => ConstDecl { 
    btype,
    const_defs,
};

ConstDef: ConstDef = <ident: Ident> "=" <const_init_val: ConstInitVal> => ConstDef {
    ident,
    const_init_val,
};

ConstInitVal: ConstInitVal = <const_exp: ConstExp> => ConstInitVal {const_exp};

VarDecl: VarDecl = <btype: BType> <var_defs: Comma<VarDef>> ";" => VarDecl {
    btype,
    var_defs,
};

VarDef: VarDef = <ident: Ident> <init_val: ("=" <InitVal>)?> => VarDef {ident, init_val};

InitVal: InitVal = <exp: Exp> => InitVal {exp};

FuncDef: FuncDef = {
  <func_type: FuncType> <ident: Ident> "(" ")" <block: Block> => {
    FuncDef {<>}
  }
};

FuncType: Type = "int" => Type::get_i32();

Block: Block = "{" <block_items: BlockItem*> "}" => Block {block_items};

BlockItem: BlockItem = {
    <decl: Decl> => BlockItem::Decl(decl),
    <stmt: Stmt> => BlockItem::Stmt(stmt),
};

Stmt: Stmt = {
    <l_val: LVal> "=" <exp: Exp> ";" => Stmt::Assign(l_val, exp),
    "return" <exp: Exp> ";" => Stmt::Return(exp),
};

LVal: LVal = <ident: Ident> => LVal {ident};

Number: i32 = <num: IntConst> => <>;

Ident: std::rc::Rc<str> = r"[_a-zA-Z][_a-zA-Z0-9]*" =>  std::rc::Rc::from(<>);

IntConst: i32 = {
  r"[1-9][0-9]*" => i32::from_str_radix(<>, 10).unwrap(),
  r"0[0-7]*" => i32::from_str_radix(<>, 8).unwrap(),
  r"0[xX][0-9a-fA-F]+" => i32::from_str_radix(&<>[2..], 16).unwrap(),
};

Exp: Exp = <lor_exp: LOrExp> => Exp {<>};

PrimaryExp: PrimaryExp = {
  "(" <exp: Exp> ")" => PrimaryExp::Exp(exp),
  <l_val: LVal> => PrimaryExp::LVal(l_val),
  <num: Number> => PrimaryExp::Number(num),
};

UnaryOp: UnaryOp = {
  "+" => UnaryOp::Add,
  "-" => UnaryOp::Minus,
  "!" => UnaryOp::Negation,
};

BinaryOp: BinaryOp = {
    "+" => BinaryOp::Add,
    "-" => BinaryOp::Sub,
    "*" => BinaryOp::Mul,
    "/" => BinaryOp::Div,
    "%" => BinaryOp::Mod,
};

UnaryExp: UnaryExp = {
  <primary_exp: PrimaryExp> => UnaryExp::PrimaryExp(Box::new(primary_exp)),
  <unary_op: UnaryOp> <unary_exp: UnaryExp> => UnaryExp::Unary(unary_op, Box::new(unary_exp)),
};

AddExpOnlyOp: BinaryOp = {
    "+" => BinaryOp::Add,
    "-" => BinaryOp::Sub,
};

AddExp: AddExp = {
    <mul_exp: MulExp> => AddExp::MulExp(mul_exp),
    <add_exp: AddExp> <op: AddExpOnlyOp> <mul_exp: MulExp> => AddExp::Comp(Box::new(add_exp), op, mul_exp),
};

MulExpOnlyOp: BinaryOp = {
    "*" => BinaryOp::Mul,
    "/" => BinaryOp::Div,
    "%" => BinaryOp::Mod,
};

MulExp: MulExp = {
    <unary_exp: UnaryExp> => MulExp::UnaryExp(<>),
    <mul_exp:MulExp> <op: MulExpOnlyOp> <unary_exp: UnaryExp> => MulExp::Comp(Box::new(mul_exp), op, unary_exp),
};

LOrExp: LOrExp = {
    <land_exp: LAndExp> => LOrExp::LAndExp(land_exp),
    <lor_exp: LOrExp> "||" <land_exp: LAndExp> => LOrExp::Comp(Box::new(lor_exp), land_exp),
};

LAndExp: LAndExp = {
    <eq_exp: EqExp> => LAndExp::EqExp(eq_exp),
    <land_exp: LAndExp> "&&" <eq_exp: EqExp> => LAndExp::Comp(Box::new(land_exp), eq_exp),
};

EqExpOnlyOp: BinaryOp = {
    "==" => BinaryOp::Eq,
    "!=" => BinaryOp::NotEq,
};

EqExp: EqExp = {
    <rel_exp: RelExp> => EqExp::RelExp(rel_exp),
    <eq_exp: EqExp> <op: EqExpOnlyOp> <rel_exp: RelExp> => EqExp::Comp(Box::new(eq_exp), op, rel_exp),
};

RelExpOnlyOp: BinaryOp = {
    "<" => BinaryOp::Lt,
    ">" => BinaryOp::Gt,
    "<=" => BinaryOp::Le,
    ">=" => BinaryOp::Ge,
};

RelExp: RelExp = {
    <add_exp: AddExp> => RelExp::AddExp(add_exp),
    <rel_exp: RelExp> <op: RelExpOnlyOp> <add_exp: AddExp> => RelExp::Comp(Box::new(rel_exp), op, add_exp),
};

ConstExp: ConstExp = <exp: Exp> => ConstExp { exp };
